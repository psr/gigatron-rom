
{-----------------------------------------------------------------------+
|       Screen test                                                     |
+-----------------------------------------------------------------------}

gcl0x

$0101 ShiftControl=

[def
  [do
    \SYS_Random_34 _sysFn= 34!! q=
    q>, 120- if>=0loop]
  128+ q>.

  q r= 72+ r<. {End of line}
  0 i=
  n= {Count, or -1 when reading new count}

  [do
    {Fill buffer with next group of 4 bytes when needed}
    n [if<=0
      i [if=0
        p 0?? \sysArgs0.
        p 1?? \sysArgs1.
        p 2?? \sysArgs2.
        p 3+ p= 255& 249^ [if=0 p 7+ p=] {Advance read pointer}
        \SYS_Unpack_56 _sysFn= 56!! {Unpack to 4 bytes}
      ]
      n
    ]

    [if<0
      \sysArgs0 i+ peek n= [if=0 ret {No more data}]
      i 1+ 3& i=
    else
      [if>0
        63 q.
        n 1- n=
        q<++
      else
        {Consume next byte}
        \sysArgs0 i+ peek [if<>0
          63^ q. {New pixel}
          q<++
        else
          -1 n=
        ]
        i 1+ 3& i=
      ]
    ]

    {Wrap write pointer}
    q r- [if=0
       0 q. {Border/shadow on the right side}
       q 72- q<. q>++ {Next line}
       q [if<0 $8800 q+ q=] {Wrap around bottom}
       q>, r>.
    ]

    \frameCount, ShiftControl.
  loop]
] Transfer=

{--- Run ---}

[do
  \gigatronRaw p=
  Transfer!
  Transfer!
  loop]

{-----------------------------------------------------------------------+
|       End                                                             |
+-----------------------------------------------------------------------}

